apply from: versions

if (isApplication()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.version_code
        versionName build_versions.version_name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        if (isApplication()) {
            applicationId "com.yeungeek.${this.name}"
        }

        resourcePrefix "${project.name}_"
        println("resourcePrefix ${project.name}_")

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (!is_debug && this.name.startsWith("module")) {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
}

//or use properties
def isApplication() {
    if (this.name.startsWith("app")) {
        println("this module is application, this.name = ${this.name}")
        return true
    }

    if (!is_debug) {
        println("debug is false")
        return false
    }

    if (!this.name.toLowerCase().startsWith("module")) {
        println("this module is not real module,this.name = ${this.name}")
        return false
    }

    println("this.module is real module, this.name = ${this.name}")
    return true
}